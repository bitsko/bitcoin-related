#!/usr/bin/bc

# basic contract hash functions

define void contracthash_sign_api(k0, tweak, z, d, *contract[]){

    auto k1, s, nonce[], sig[];

    ecmul_api(k0, curve_gx, curve_gy, curve_n, curve_p, nonce[]);
    k1 = mod(k0 + tweak, curve_n);
    ecdsa_sign(k1, z, d, sig[]);
    contract[0] = nonce[0];
    contract[1] = nonce[1];
    contract[2] = sig[0];
    contract[3] = sig[1];
}

define contracthash_verify_api(z, pub, nonce, dersig, tweak){

    auto tw_pt[], cmt_pt[], nnc_pt[], pub_pt[], sig[], r_ver;

    uncompresspoint_api(nonce, nnc_pt[]);
    uncompresspoint_api(pub, pub_pt[]);
    ecdsa_der2sig(dersig, sig[]);
    ecmul_api(tweak, curve_gx, curve_gy, curve_n, curve_p, tw_pt[]);
    ecadd_api(tw_pt[0], tw_pt[1], nnc_pt[0], nnc_pt[1], curve_p, cmt_pt[])
    r_ver = ecdsa_verify_api(z, pub_pt[0], pub_pt[1], sig[0], sig[1], curve_gx, curve_gy, curve_p, curve_n);
    return (sig[0] != 0) && (r_ver == cmt_pt[0]);
}

# used for recovering k and d from contracthash nonce reuse

define contracthash_recover_k_api(r1, s1, tw1, r2, s2, tw2, z1, n){

    auto r1i;

    r1i = invmod(r1, n);
    return mod( (z1*(1 - r1i*r2) + s2*(tw1 - tw2)) * invmod(s2 - s1*r1i*r2, n), n)
}

define void contracthash_recover_k(z1, dersig1, tw1, dersig2, tw2, n){

    auto sig1[], sig2[], k1, k2, k3, k4;

    ecdsa_der2sig(dersig1, sig1[]);
    ecdsa_der2sig(dersig2, sig2[]);
    k1 = contracthash_recover_k_api(sig1[0], sig1[1], tw1, sig2[0], sig2[1], tw2, z1, n);
    k2 = contracthash_recover_k_api(sig1[0], -sig1[1], tw1, sig2[0], sig2[1], tw2, z1, n);
    k3 = contracthash_recover_k_api(sig1[0], sig1[1], tw1, sig2[0], -sig2[1], tw2, z1, n);
    k4 = contracthash_recover_k_api(sig1[0], -sig1[1], tw1, sig2[0], -sig2[1], tw2, z1, n);

    print "\npossible k values :\n\n";
    k1; k2; k3; k4;

    print "\npossible d values :\n\n";
    mod(mod((sig1[1]*k1)-z1,n) * invmod(sig1[0],n),n);
    mod(mod((sig1[1]*k2)-z1,n) * invmod(sig1[0],n),n);
    mod(mod((sig1[1]*k3)-z1,n) * invmod(sig1[0],n),n);
    mod(mod((sig1[1]*k4)-z1,n) * invmod(sig1[0],n),n);
}
